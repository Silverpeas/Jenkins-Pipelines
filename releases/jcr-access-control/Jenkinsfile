/*
 * This pipeline requires the following build parameter to be set:
 * BUILD_VERSION: the version number of an existing Silverpeas build from which the release will be performed
 */
node {
  catchError {
    def maven = 'Maven 3.3'
    def java = 'Java Oracle 8'
    def nexusRepo = 'https://www.silverpeas.org/nexus/content/repositories/builds'
    def version
    stage('Preparation') {
      echo "Prepare the release of JCR Access Control ${version}..."
      // Get the Maven tool.
      version = getVersionNumber(params.BUILD_VERSION)
      deleteDir()
    }
    stage('Release') {
      echo "Release of JCR Access Control ${version}..."
      withMaven(jdk: java, maven: maven) {
        // Fetch the Git commit corresponding to the build number
        sh "wget ${nexusRepo}/org/silverpeas/jcr/access-control/${params.BUILD_VERSION}/access-control-${params.BUILD_VERSION}.jar"
        def manifest = readManifest file: "${env.WORKSPACE}/access-control-${params.BUILD_VERSION}.jar"
        def commit = manifest.main['Git-Commit']

        // checkout the project
        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-JCR-AccessControl.git'

        // release the project
        releaseProject(commit, version)
        prepareNextSnapshotVersion(commit, version)
      }
    }
  }
  step([$class: 'Mailer',
        notifyEveryUnstableBuild: true,
        recipients: "miguel.moquillon@silverpeas.org",
        sendToIndividuals: true])
}

def buildMavenProjectSnapshot(nextVersion) {
  sh """
sed -i -e "s/<next.release>[0-9.]\\+/<next.release>${nextVersion}/g" pom.xml
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${nextVersion}-SNAPSHOT
mvn clean install -Dmaven.test.skip=true
"""
}

def buildMavenProjectRelease(version) {
  sh """
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${version}
mvn clean deploy -Dmaven.test.skip=true -Prelease-sign-artifacts
"""
}

def prepareNextSnapshotVersion(commit, version) {
  echo "-> Prepare Next Snapshot version after ${version}"
  def versionParts = version.split('\\.')
  def nextversion = "${versionParts[0]}.${(versionParts[1] as Integer) + 1}"

  echo "Prepare the next development iteration for ${nextversion}"
  sh "git checkout master"
  buildMavenProjectSnapshot(nextversion)
  sh """
git commit -am "${version} has been released from commit ${commit} (build version ${params.BUILD_VERSION}). Prepare for next development iteration of next version ${nextversion}"
git push origin master
"""
}

def releaseProject(commit, version) {
  echo "-> Release project to version ${version}"
  // we first change the Maven repo definition to use the repo dedicated to releases
  // then we release the project and finally we tag it
  sh """
git checkout ${commit}
git checkout -b release-${version}
"""
  buildMavenProjectRelease(version)
  sh """
git commit -am "Release of ${version}"
git tag ${version}
git push origin --tags
"""
}

@NonCPS
def getVersionNumber(buildVersion) {
  def matcher = buildVersion =~ '(.+)-build\\d+'
  matcher ? matcher[0][1] : null
}