/*
 * This pipeline requires the following build parameter to be set:
 * BRANCH: the name of the Git branch from which the project has to be fetched and built.
 */
node {
  catchError {
    def nexusRepo = 'https://www.silverpeas.org/nexus/content/repositories/builds'
    def version
    def parentVersion
    def release
    def commits = [:]
    docker.image("silverpeas/silverbuild:${params.IMAGE_VERSION}").inside('-v $HOME/.m2/settings.xml:/home/silverbuild/.m2/settings.xml -v $HOME/.m2/settings-security.xml:/home/silverbuild/.m2/settings-security.xml -v $HOME/.gitconfig:/home/silverbuild/.gitconfig -v $HOME/.ssh:/home/silverbuild/.ssh -v $HOME/.gnupg:/home/silverbuild/.gnupg') {
      stage('Preparation') {
        sh "rm -rf *"
        if (!params.BRANCH) {
          params.BRANCH = 'master'
        }
        def buildNumber = (new Date()).format('yyMMdd')
        sh "curl -fsSL -o pom.xml https://raw.githubusercontent.com/Silverpeas/Silverpeas-Core/${params.BRANCH}/pom.xml"
        def pom = readMavenPom()
        release = pom.properties['next.release']
        version = "${release}-build${buildNumber}"
        if (params.BRANCH == 'master' && (pom.parent.version.contains('build') || pom.parent.version.contains('SNAPSHOT'))) {
          copyArtifacts projectName: 'Silverpeas_Project_Definition_AutoDeploy', flatten: true
          def parentBuild = readYaml file: 'build.yaml'
          parentVersion = parentBuild.version
        } else {
          parentVersion = pom.parent.version
        }
        echo "Parent version is ${parentVersion}"
        echo "Trigger build version ${version} of Silverpeas from ${params.BRANCH}..."
      }
      stage('Checkout Silverpeas Core') {
        fetchFromGithub 'Silverpeas-Core'
      }
      stage('Checkout Silverpeas Components') {
        fetchFromGithub 'Silverpeas-Components'
      }
      stage('Checkout Silverpeas Assemby') {
        fetchFromGithub 'Silverpeas-Assembly'
      }
      stage('Checkout Silverpeas Setup') {
        fetchFromGithub 'Silverpeas-Setup'
      }
      stage('Checkout Silverpeas Distribution') {
        fetchFromGithub 'Silverpeas-Distribution'
      }
      stage('Checkout Silverpeas Looks') {
        fetchFromGithub 'Silverpeas-Looks'
      }
      stage('Checkout Silverpeas Mobile') {
        fetchFromGithub 'silverpeasmobile'
      }
      stage('Build Silverpeas Core') {
        buildMavenProject(project: 'Silverpeas-Core',
            version: version,
            parentVersion: parentVersion) {
          runSonarAnalysis(it)
        }
      }
      stage('Build Silverpeas Components') {
        buildMavenProject(project: 'Silverpeas-Components',
            version: version,
            parentVersion: parentVersion) {
          runSonarAnalysis(it)
        }
      }
      stage('Build Silverpeas Assembly') {
        buildMavenProject project: 'Silverpeas-Assembly',
            version: version,
            parentVersion: parentVersion
      }
      stage('Build Silverpeas Setup') {
        buildGradleProject project: 'Silverpeas-Setup',
            version: version
      }
      stage('Build Silverpeas Distribution') {
        buildMavenProject project: 'Silverpeas-Distribution',
            version: version
      }
      stage('Build Silverpeas Looks') {
        buildMavenProject(project: 'Silverpeas-Looks',
            version: version,
            parentVersion: parentVersion) {
          runSonarAnalysis(it)
        }
      }
      stage('Build Silverpeas Mobile') {
        buildMavenProject project: 'silverpeasmobile',
            version: version,
            parentVersion: parentVersion
      }
      stage('Deploy Silverpeas Core') {
        deployMavenProject(project: 'Silverpeas-Core',
            repo: nexusRepo,
            commits: commits)
      }
      stage('Deploy Silverpeas Components') {
        deployMavenProject(project: 'Silverpeas-Components',
            repo: nexusRepo,
            commits: commits)
      }
      stage('Deploy Silverpeas Assembly') {
        deployMavenProject(project: 'Silverpeas-Assembly',
            repo: nexusRepo,
            commits: commits)
      }
      stage('Deploy Silverpeas Setup') {
        deployGradleProject(project: 'Silverpeas-Setup',
            repo: nexusRepo,
            commits: commits)
      }
      stage('Deploy Silverpeas Distribution') {
        deployMavenProject(project: 'Silverpeas-Distribution',
            repo: nexusRepo,
            commits: commits)
      }
      stage('Deploy Silverpeas Looks') {
        deployMavenProject(project: 'Silverpeas-Looks',
            repo: nexusRepo,
            commits: commits)
      }
      stage('Deploy Silverpeas Mobile') {
        deployMavenProject(project: 'silverpeasmobile',
            repo: nexusRepo,
            commits: commits)
      }
      stage('Archive Build Version') {
        sh "mkdir -p target"
        final String artifact = 'target/build.yaml'
        writeYaml file: artifact, data: ['version': version,
                                         'parent': parentVersion,
                                         'release': release,
                                         'commits': commits]
        archiveArtifacts artifacts: artifact, fingerprint: true
        currentBuild.displayName = version
      }
      stage('Publish Project Web Site') {
        // publish the project web site only for the master branch
        if (params.BRANCH == 'master') {
          echo "Publish the web site with the new build version ${version}..."
          fetchFromGithub('Silverpeas-Project-Web-Site')
          dir("Silverpeas-Project-Web-Site") {
            def status = sh script: """
sed -i -e "s/<installer_build_version>[0-9a-zA-Z.-]\\+/<installer_build_version>${version}/g" pom.xml
mvn clean site-deploy
""", returnStatus: true
            if (status != 0) {
              emailext(
                  subject: "Silverpeas Web Site publishing failure!",
                  body: """
I was unable to publish the Silverpeas Community Web Site to www.silverpeas.org!
Please, consult the console log of the build ${currentBuild.displayName} of ${
                    currentBuild.projectName
                  }
""",
                  to: 'miguel.moquillon@silverpeas.org, yohann.chastagnier@silverpeas.org'
              )
            }
          }
        }
      }
    }
  }
  step([$class: 'Mailer',
        notifyEveryUnstableBuild: true,
        recipients: "miguel.moquillon@silverpeas.org, yohann.chastagnier@silverpeas.org, nicolas.eysseric@silverpeas.org",
        sendToIndividuals: true])
  step([$class: 'Mailer',
        notifyEveryUnstableBuild: false,
        recipients: "nicolas.eysseric@silverpeas.org, david.lesimple@silverpeas.org, sebastien.vuillet@silverpeas.org",
        sendToIndividuals: true])
}

def fetchFromGithub(project) {
  dir (project) {
    git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', branch: params.BRANCH, poll: false, url: "https://github.com/Silverpeas/${project}"
  }
}

def pushToGithub(project, branch) {
  withCredentials([usernamePassword(credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59',
      usernameVariable: 'USER', passwordVariable: 'PASSWORD')]) {
    sh "git push https://${USER}:${PASSWORD}@github.com/Silverpeas/${project} HEAD:${branch}"
  }
}

def buildGradleProject(args, postbuild = null) {
  dir(args.project) {
    sh """
sed -i -e "s/version = '.\\+'/version = '${args.version}'/g" build.gradle
./gradlew clean test install
"""
    //stash name: args.project, includes: '**/build.gradle, **/build/**/*'
    postbuild?.call()
  }
}

def buildMavenProject(args, postbuild = null) {
  dir(args.project) {
    def pom = readMavenPom()
    if (pom.parent) {
      if (!pom.parent.version.matches(/[0-9.]+/) && args.parentVersion && pom.parent.version != args.parentVersion) {
        sh """
mvn -U versions:update-parent -DgenerateBackupPoms=false -DparentVersion=${args.parentVersion}
git commit -am "Update parent POM to version ${args.parentVersion}"
"""
        pushToGithub(args.project, params.BRANCH)
      } else if (pom.parent.version.contains('SNAPSHOT')) {
        error "The parent POM must be at a stable or a build version for this project to be deployed. Current version is ${pom.parent.version}"
      }
    }
    if (args.project == 'Silverpeas-Assembly' && pom.properties['jackrabbit.version'].contains('SNAPSHOT')) {
      error("The Jackrabbit JCA dependency must be a stable or a build version for this project to be deployed. Current version is ${pom.properties['jackrabbit.version']}")
    }
    sh """
sed -i -e "s/<silverpeas.version>[0-9a-zA-Z.-]\\+/<silverpeas.version>${args.version}/g" pom.xml
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${args.version}
"""
    if (args.version.startsWith('6.0')) {
      sh """
/usr/local/bin/ooserver start
mvn clean install -Pdeployment -Djava.awt.headless=true -Dcontext=ci -Dmaven.test.skip=true
/usr/local/bin/ooserver stop
"""
    } else {
      sh "mvn clean install -Pdeployment -Djava.awt.headless=true -Dcontext=ci"
    }
    postbuild?.call(args.project)
  }
}

def runSonarAnalysis(project) {
  // run sonarqube analysis only for the master branch
  if (params.BRANCH == 'master') {
    withSonarQubeEnv {
      sh """
mvn ${SONAR_MAVEN_GOAL} -Dsonar.projectKey=Silverpeas_${project} \\
    -Dsonar.organization=silverpeas \\
    -Dsonar.host.url=${SONAR_HOST_URL} \\
    -Dsonar.login=${SONAR_AUTH_TOKEN}
"""
    }
    timeout(time: 10, unit: 'MINUTES') {
      // Just in case something goes wrong, pipeline will be killed after a timeout
      def qg = waitForQualityGate() // Reuse taskId previously collected by withSonarQubeEnv
      if (qg.status != 'OK' && qg.status != 'WARNING') {
        error "Pipeline aborted due to quality gate failure: ${qg.status}"
      }
    }
  }
}

def deployGradleProject(args) {
  dir(args.project) {
    final String target = fetchTargetRepo(args.repo)
    sh """
sed -i -e "s/repositories\\/releases/${target.replaceAll('/', '\\\\/')}/g" build.gradle
./gradlew publish
"""
    final String commit = sh script: 'git rev-parse HEAD', returnStdout: true
    args.commits[args.project] = commit.trim()
  }
}

def deployMavenProject(args) {
  dir(args.project) {
    sh "mvn deploy -DaltDeploymentRepository=silverpeas::default::${args.repo} -Pdeployment -Djava.awt.headless=true -Dmaven.test.skip=true"
    final String commit = sh script: 'git rev-parse HEAD', returnStdout: true
    args.commits[args.project] = commit.trim()
  }
}

@NonCPS
def fetchTargetRepo(nexusRepoUrl) {
  def matcher = nexusRepoUrl =~ /(repositories\/.+)/
  matcher ? matcher[0][1] : ''
}

