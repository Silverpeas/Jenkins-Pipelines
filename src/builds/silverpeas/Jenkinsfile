node {
  catchError {
    def maven = 'Maven 3.3'
    def java = 'Java Oracle 8'
    def nexusRepo = 'https://www.silverpeas.org/nexus/content/repositories/builds'
    def version
    stage('Preparation') {
      def buildNumber = (new Date()).format('yyMMdd')
      echo "Prepare the build ${buildNumber} of Silverpeas..."
      deleteDir()
      sh "wget https://raw.githubusercontent.com/Silverpeas/Silverpeas-Core/master/pom.xml"
      def pom = readMavenPom()
      version = "${pom.properties['next.release']}-build${buildNumber}"
    }
    stage('Build Silverpeas Core') {
      echo "Build ${version} of Silverpeas Core..."
      withMaven(jdk: java, maven: maven) {
        buildMavenProject('Silverpeas-Core', version)
        runSonarAnalysis()
      }
    }
    stage('Build Silverpeas Components') {
      echo "Build ${version} of Silverpeas Components..."
      withMaven(jdk: java, maven: maven) {
        buildMavenProject('Silverpeas-Components', version)
        runSonarAnalysis()
      }
    }
    stage('Build Silverpeas Assembly') {
      echo "Build ${version} of Silverpeas Assembly..."
      withMaven(jdk: java, maven: maven) {
        buildMavenProject('Silverpeas-Assembly', version) { pom ->
          if (pom.properties['jackrabbit.version'].contains('SNAPSHOT')) {
            error ("The Jackrabbit JCA dependency must be a stable or a build version for this project to be deployed. Current version is ${pom.properties['jackrabbit.version']}")
          }
          if (pom.parent.version.contains('SNAPSHOT')) {
            error "The parent POM must be at a stable or a build version for this project to be deployed. Current version is ${pom.parent.version}"
          }
        }
      }
    }
    stage('Build Silverpeas Setup') {
      echo "Build ${version} of Silverpeas Setup..."
      buildGradleProject('Silverpeas-Setup', version)
    }
    stage('Build Silverpeas Distribution') {
      echo "Build ${version} of Silverpeas Distribution..."
      withMaven(jdk: java, maven: maven) {
        buildMavenProject('Silverpeas-Distribution', version)
      }
    }
    /*stage('Silverpeas Looks') {
      echo "Build ${version} of Silverpeas Looks..."
      withMaven(jdk: java, maven: maven) {
        buildMavenProject('Silverpeas-Looks', version)
      }
    }*/
    stage('Publish Project Web Site') {
      echo "Publish the web site with the new build version ${version}..."
      withMaven(jdk: java, maven: maven) {
        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Project-Web-Site'
        sh """
sed -i -e "s/<installer_build_version>[0-9a-zA-Z.-]\\+/<installer_build_version>${version}/g" pom.xml
mvn clean site-deploy
"""
      }
    }
    stage('Deploy Build Version') {
      withMaven(jdk: java, maven: maven) {
        echo "Deploy Silverpeas Core ${version}..."
        deployMavenProject('Silverpeas-Core', nexusRepo)

        echo "Deploy Silverpeas Components ${version}..."
        deployMavenProject('Silverpeas-Components', nexusRepo)

        echo "Deploy Silverpeas Assembly ${version}..."
        deployMavenProject('Silverpeas-Assembly', nexusRepo)

        echo "Deploy Silverpeas Setup ${version}..."
        deployGradleProject('Silverpeas-Setup', nexusRepo)

        echo "Deploy Silverpeas Distribution ${version}..."
        deployMavenProject('Silverpeas-Distribution', nexusRepo)
      }
    }
  }
  step([$class: 'Mailer',
        notifyEveryUnstableBuild: true,
        recipients: "miguel.moquillon@silverpeas.org",
        sendToIndividuals: true])
}

def buildGradleProject(project, version, checker) {
  fetchProjectFromGithub(project)
  if (checker && checker instanceof Closure) {
    checker()
  }
  sh """
sed -i -e "s/version = '.\\+'/version = '${version}'/g" build.gradle
./gradlew clean test install
"""
  stash name: project, includes: '**/build.gradle, **/build/**/*'
}

def buildMavenProject(project, version, checker) {
  fetchProjectFromGithub(project)
  def pom = readMavenPom()
  if (pom.parent != null && pom.parent.version.contains('SNAPSHOT')) {
    error "The parent POM must be at a stable or a build version for this project to be deployed. Current version is ${pom.parent.version}"
  }
  if (checker && checker instanceof Closure) {
    checker(pom)
  }
  sh """
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${version}
mvn clean install -Pdeployment -Djava.awt.headless=true -Dcontext=ci
"""
  stash name: project, includes: '**/pom.xml, **/target/**/*'
}

def runSonarAnalysis() {
  withSonarQubeEnv {
    sh "mvn ${SONAR_MAVEN_GOAL} -Dsonar.login=${SONAR_AUTH_TOKEN} -Dsonar.host.url=${SONAR_HOST_URL}"
  }
}

def deployGradleProject(project, repo) {
  fetchProjectFromGithub(project)
  unstash name: project
  def target = fetchTargetRepo(repo)
  sh """
sed -i -e 's/repositories\\\\/releases/${target.replaceAll('/', '\\\\/')}/g' build.gradle
./gradlew publish
"""
}

def deployMavenProject(project, repo) {
  fetchProjectFromGithub(project)
  unstash name: project
  sh "mvn deploy -DaltDeploymentRepository=silverpeas::default::${repo} -Pdeployment -Djava.awt.headless=true -Dmaven.test.skip=true"
}

def fetchProjectFromGithub(project) {
  deleteDir()
  git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: "https://github.com/Silverpeas/${project}"
}

@NonCPS
def fetchTargetRepo(nexusRepoUrl) {
  def matcher = nexusRepoUrl =~ /(repositories\/.+)/
  matcher ? matcher[0][1] : ''
}

