/*
 * This pipeline requires the following build parameter to be set:
 * BUILD_VERSION: the version number of an existing Silverpeas build from which the release will be performed
 */
node {
  catchError {
    def maven = 'Maven 3.5'
    def java = 'Java Oracle 8'
    def nexusRepo = 'https://www.silverpeas.org/nexus/content/repositories/builds'
    def wildflyVersion = '10.1.0'
    def version
    stage('Preparation') {
      version = getVersionNumber(params.BUILD_VERSION)
      echo "Prepare the release of Silverpeas ${version}..."

    }
    stage('Silverpeas Core') {
      echo "Release Silverpeas Core ${version}..."
      withMaven(jdk: java, maven: maven) {
        def commit =
            getCommitHash("${nexusRepo}/org/silverpeas/core/silverpeas-core-api/${params.BUILD_VERSION}/silverpeas-core-api-${params.BUILD_VERSION}.jar")

        // checkout the project
        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Core'

        // release the project
        releaseProject(commit, version)
        prepareNextSnapshotVersion(commit, version)
      }
    }
    stage('Silverpeas Components') {
      echo "Release Silverpeas Components ${version}..."
      withMaven(jdk: java, maven: maven) {
        def commit =
            getCommitHash("${nexusRepo}/org/silverpeas/components/kmelia/silverpeas-kmelia/${params.BUILD_VERSION}/silverpeas-kmelia-${params.BUILD_VERSION}.jar")

        // checkout the project
        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Components'

        // release the project
        releaseProject(commit, version)
        prepareNextSnapshotVersion(commit, version)
      }
    }
    stage('Silverpeas Assembly') {
      echo "Release Silverpeas Assembly ${version}..."
      withMaven(jdk: java, maven: maven) {
        // checkout the project
        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Assembly'

        sh """
git checkout -b pre-release-${version}
wget ${nexusRepo}/org/silverpeas/silverpeas-assembly/${params.BUILD_VERSION}/silverpeas-assembly-${params.BUILD_VERSION}.pom -O pom.xml
git commit -am "Prepare the release of ${version}"
"""
        // release the project
        def pom = readMavenPom()
        if (pom.properties['jackrabbit.version'].contains('build') || pom.properties['jackrabbit.version'].contains('SNAPSHOT')) {
          error ("The Jackrabbit JCA dependency must be a stable version for this project to be released. Current version is ${pom.properties['jackrabbit.version']}")
        }
        releaseProject("pre-release-${version}", version)
        prepareNextSnapshotVersion('master', version)
      }
    }
    stage('Silverpeas Setup') {
      echo "Release Silverpeas Setup ${version}..."
      def commit =
          getCommitHash("${nexusRepo}/org/silverpeas/silverpeas-setup/${params.BUILD_VERSION}/silverpeas-setup-${params.BUILD_VERSION}.jar")

      deleteDir()
      git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Setup'

      releaseProject(commit, version)
      prepareNextSnapshotVersion(commit, version)
    }
    stage('Silverpeas Distribution') {
      echo "Release Silverpeas Distribution ${version}..."
      withMaven(jdk: java, maven: maven) {
        def commit = getDistribCommitHash(params.BUILD_VERSION, nexusRepo)

        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Distribution'

        releaseProject(commit, version)
        prepareNextSnapshotVersion(commit, version)
      }
    }
    stage('Silverpeas Looks') {
      echo "Release Silverpeas Looks ${version}..."
      withMaven(jdk: java, maven: maven) {
        def commit =
            getCommitHash("${nexusRepo}/org/silverpeas/looks/aurora/silverpeas-aurora-configuration/${params.BUILD_VERSION}/silverpeas-aurora-configuration-${params.BUILD_VERSION}.jar")

        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Looks'

        releaseProject(commit, version)
        prepareNextSnapshotVersion(commit, version)
      }
    }
    stage('Silverpeas Mobile') {
      echo "Release Silverpeas Mobile ${version}..."
      withMaven(jdk: java, maven: maven) {
        def commit =
            getCommitHash("${nexusRepo}/org/silverpeas/components/mobile/silverpeas-mobile-configuration/${params.BUILD_VERSION}/silverpeas-mobile-configuration-${params.BUILD_VERSION}.jar")

        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/silverpeasmobile'

        releaseProject(commit, version)
        prepareNextSnapshotVersion(commit, version)
      }
    }
    stage('Silverpeas Izpack Installer') {
      echo "Generate the Izpack installer for Silverpeas ${version}"
      withMaven(jdk: java, maven: maven) {
        deleteDir()
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Izpack-Installer'
        def branch = getMainBranch(version)
        sh "git checkout ${branch}"
        def pom = readMavenPom()
        if (pom.parent.version.contains('build') || pom.parent.version.contains('SNAPSHOT')) {
          error "The version of parent POM must be a stable version for this project to be released. Current version is ${pom.parent.version}"
        }
        sh """
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${version}
mvn clean install
git commit -am "Build and deliver the Izpack installer for Silverpeas ${version}"
git tag ${version}
git push origin HEAD:master
git push --tags
"""
        if (!isMinor(version)) {
          sh """
git checkout -b ${version}.x
git commit -am "Prepare branch ${version}.x for minor versions"
git push origin ${version}.x
"""
        }
      }
    }
    stage('Silverpeas Test Docker Image') {
      echo "Generate a Test Docker image of Silverpeas ${version}"
      deleteDir()
      git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/docker-silverpeas-test'

      sh """
sed -i -e "s/ARG SILVERPEAS_VERSION=.\\+/ARG SILVERPEAS_VERSION=${version}/g" Dockerfile
sed -i -e "s/build=.\\+/build=1/g" Dockerfile
git commit -am "Update Dockerfile for Silverpeas ${version}"
git tag ${version}
git push origin HEAD:master
git push --tags
"""
    }
    stage('Silverpeas Docker Image') {
      echo "Generate a Docker image of Silverpeas ${version}"
      deleteDir()
      git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/docker-silverpeas-prod'

      sh """
./generate-dockerfile.sh ${version} ${wildflyVersion}
git commit -am "Generate Dockerfile for Silverpeas ${version}"
git tag ${version}
git push origin HEAD:master
git push --tags
./generate-docker-library.sh > silverpeas
"""
      stash includes: 'silverpeas', name: 'docker-library'

      deleteDir()
      git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/official-images'

      dir('library') {
        unstash 'docker-library'
      }
      sh """
git commit -am "Update Silverpeas Docker image version ${version}"
git push origin HEAD:master
"""
    }
    stage('Publish Project Web Site') {
      echo "Publish the web site with the new build version ${version}..."
      copyArtifacts projectName: 'Silverpeas_Master_AutoDeploy',
          filter: "org/silverpeas/core/*-build*/*.pom",
          flatten: true
      def files = findFiles glob: '*.pom'
      def pom = readMavenPom file: files[0].path
      def lastBuildVersion = pom.version
      deleteDir()
      git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: 'https://github.com/Silverpeas/Silverpeas-Project-Web-Site'
      withMaven(jdk: java, maven: maven) {
          sh """
sed -i -e "s/<installer_stable_version>[0-9a-zA-Z.-]\\+/<installer_stable_version>${version}/g" pom.xml
sed -i -e "s/<installer_izpack_version>[0-9a-zA-Z.-]\\+/<installer_izpack_version>${version}/g" pom.xml
sed -i -e "s/<installer_build_version>[0-9a-zA-Z.-]\\+/<installer_build_version>${lastBuildVersion}/g" pom.xml
mvn clean site-deploy
"""
      }
    }
  }
  step([$class: 'Mailer',
        notifyEveryUnstableBuild: true,
        recipients: "miguel.moquillon@silverpeas.org",
        sendToIndividuals: true])
}

def buildGradleProject(version) {
  sh """
sed -i -e "s/version = '.\\+'/version = '${version}'/g" build.gradle
./gradlew clean install publish
"""
}

def buildMavenProjectSnapshot(nextVersion) {
  sh """
sed -i -e "s/<silverpeas.version>[0-9a-zA-Z.-]\\+/<silverpeas.version>${nextVersion}-SNAPSHOT/g" pom.xml
sed -i -e "s/<next.release>[0-9.]\\+/<next.release>${nextVersion}/g" pom.xml
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${nextVersion}-SNAPSHOT
mvn clean install -Dmaven.test.skip=true -Pdeployment
"""
}

def buildMavenProjectRelease(version) {
  def pom = readMavenPom()
  if (pom.parent != null && (pom.parent.version.contains('build') || pom.parent.version.contains('SNAPSHOT'))) {
    error "The version of parent POM must be a stable version for this project to be released. Current version is ${pom.parent.version}"
  }
  sh """
sed -i -e "s/<silverpeas.version>[0-9a-zA-Z.-]\\+/<silverpeas.version>${version}/g" pom.xml
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${version}
mvn clean deploy -Dmaven.test.skip=true -Pdeployment -Prelease-sign-artifacts
"""
}

def createMinorBranchFor(version) {
  sh "git checkout -b ${version}.x"
  if (fileExists('pom.xml')) {
    buildMavenProjectSnapshot("${version}.1")
  } else {
    buildGradleProject("${version}.1")
  }
  sh """
git commit -am "Prepare branch ${version}.x for bug fixes"
git push origin ${version}.x
"""
}

def prepareNextSnapshotVersion(commit, version) {
  echo "-> Prepare Next Snapshot version after ${version}"
  def nextversion = getNextVersion(version)
  def mainbranch = getMainBranch(version)
  if (!isMinor(version)) {
    echo "Create bug-fix branch ${version}.x"
    createMinorBranchFor(version)
  }

  echo "Prepare the next development iteration for ${nextversion}"
  sh "git checkout ${mainbranch}"
  if (fileExists('pom.xml')) {
    buildMavenProjectSnapshot(nextversion)
  } else {
    buildGradleProject("${nextversion}-SNAPSHOT")
  }
  sh """
git commit -am "${version} has been released from commit ${commit} (build version ${params.BUILD_VERSION}). Prepare for development iteration of next version ${nextversion}"
git push origin ${mainbranch}
"""
}

def releaseProject(commit, version) {
  echo "-> Release project to version ${version}"
  // we first change the Maven repo definition to use the repo dedicated to releases
  // then we release the project and finally we tag it
  sh """
git checkout ${commit}
git checkout -b release-${version}
"""
  if (fileExists('pom.xml')) {
    buildMavenProjectRelease(version)
  } else {
    buildGradleProject(version)
  }
  sh """
git commit -am "Release of ${version}"
git tag ${version}
git push origin --tags
"""
}

def getCommitHash(artifactUrl) {
  String artifactName = getArtifactName(artifactUrl)
  sh "wget ${artifactUrl}"
  def manifest = readManifest file: "${artifactName}"
  manifest.main['Git-Commit']
}

def getDistribCommitHash(buildVersion, repo) {
  sh """
wget ${repo}/org/silverpeas/silverpeas-distribution/${buildVersion}/silverpeas-distribution-${buildVersion}.zip
unzip -j silverpeas-distribution-${buildVersion}.zip silverpeas*/BUILD
"""
  def buildProps = readProperties file: 'BUILD'
  buildProps['Git-Commit']
}

@NonCPS
def getArtifactName(artifactUrl) {
  def matcher = artifactUrl =~ '^.+/(.+\\.jar)$'
  matcher ? matcher[0][1] : null
}

@NonCPS
def getVersionNumber(buildVersion) {
  def matcher = buildVersion =~ '(.+)-build\\d+'
  matcher ? matcher[0][1] : null
}

@NonCPS
def isMinor(version) {
  (version =~ '\\d+.\\d+.\\d+').matches()
}

@NonCPS
def getNextVersion(version) {
  String[] parts = version.split('\\.')
  parts.length < 3 ? "${parts[0]}.${(parts[1] as Integer) + 1}" : "${parts[0]}.${parts[1]}.${(parts[2] as Integer) + 1}"
}

@NonCPS
def getMainBranch(version) {
  def matcher = version =~ '^(\\d+.\\d+)\\..*$'
  matcher ? "${matcher[0][1]}.x" : 'master'
}
