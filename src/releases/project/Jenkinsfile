node {
  catchError {
    def version
    def nextVersion
    def branch
    docker.image("silverpeas/silverbuild")
        .inside('-v $HOME/.m2/settings.xml:/home/silverbuild/.m2/settings.xml -v $HOME/.m2/settings-security.xml:/home/silverbuild/.m2/settings-security.xml -v $HOME/.gitconfig:/home/silverbuild/.gitconfig -v $HOME/.ssh:/home/silverbuild/.ssh -v $HOME/.gnupg:/home/silverbuild/.gnupg') {
      stage('Preparation') {
        echo "Prepare the release of Silverpeas Project (and the dependencies definitions)..."
        branch = params.BRANCH ? params.BRANCH : 'master'
        if (branch == 'master') {
          copyArtifacts projectName: 'Silverpeas_Project_Definition_AutoDeploy', flatten: true
          def parentBuild = readYaml file: 'build.yaml'
          version = parentBuild.release
          nextVersion = getNextVersion(parentBuild.release)
        } else {
          sh "curl -fsSL -o pom.xml https://raw.githubusercontent.com/Silverpeas/Silverpeas-Project/${branch}/pom.xml"
          def pom = readMavenPom()
          version = pom.properties['next.release']
          nextVersion = getNextVersion(version)
        }
        echo "Release of ${version}. The next version will be ${nextVersion}"
      }
      stage('Silverpeas Dependencies') {
        echo "Release Silverpeas Dependencies ${version}..."
        fetchFromGithub('silverpeas-dependencies-bom', branch)
        def pom = readMavenPom()
        if (pom.properties['jcr.accesscontrol.version'].contains('SNAPSHOT') || pom.properties['jcr.accesscontrol.version'].contains('build')) {
          error "The Silverpeas JCR AccessControl dependency must be a stable version. Current version is ${pom.properties['jcr.accesscontrol.version']}"
        }
        releaseVersion(version)
        prepareNextVersion(nextVersion)
        pushToGithub('silverpeas-dependencies-bom', branch)
      }
      stage('Silverpeas Test Dependencies') {
        echo "Release Silverpeas Test Dependencies ${version}..."
        fetchFromGithub('silverpeas-test-dependencies-bom', branch)
        releaseVersion(version)
        prepareNextVersion(nextVersion)
        pushToGithub('silverpeas-test-dependencies-bom', branch)
      }
      stage('Silverpeas Project') {
        echo "Release ${version} Silverpeas Project ${version}..."
        fetchFromGithub('Silverpeas-Project', branch)
        sh """
sed -i -e "s/<silverpeas.bom.version>[0-9a-zA-Z.-]\\+/<silverpeas.bom.version>${version}/g" pom.xml
"""
        releaseVersion(version)

        sh """
sed -i -e "s/<silverpeas.bom.version>${version}/<silverpeas.bom.version>${nextVersion}-SNAPSHOT/g" pom.xml
"""
        prepareNextVersion(nextVersion)
        pushToGithub('Silverpeas-Project', branch)
      }
    }
  }
  step([$class: 'Mailer',
        notifyEveryUnstableBuild: true,
        recipients: "miguel.moquillon@silverpeas.org, yohann.chastagnier@silverpeas.org",
        sendToIndividuals: true])
}

def releaseVersion(version) {
  echo "-> Release version ${version}"
  sh """
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${version}
sed -i -e "s/<tag>[a-zA-Z0-9.\\-]\\+/<tag>${version}/g" pom.xml
mvn clean deploy -Djava.awt.headless=true
git commit -am "Release of ${version}"
git tag ${version}
"""
}

def prepareNextVersion(nextversion) {
  echo "-> Prepare Next Snapshot version ${nextversion}"
  sh """
sed -i -e "s/<next.release>[0-9.]\\+/<next.release>${nextversion}/g" pom.xml
sed -i -e "s/<tag>[a-zA-Z0-9.\\-]\\+/<tag>HEAD/g" pom.xml
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${nextversion}-SNAPSHOT
mvn clean install -Djava.awt.headless=true
git commit -am "Prepare for next development iteration of next version ${nextversion}"
"""
}

def fetchFromGithub(project, branch) {
  sh "rm -rf * .git*"
  git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, branch: branch,
      url: "https://github.com/Silverpeas/${project}"
}

def pushToGithub(project, branch) {
  withCredentials([usernamePassword(credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59',
      usernameVariable: 'USER', passwordVariable: 'PASSWORD')]) {
    sh """
git push https://${USER}:${PASSWORD}@github.com/Silverpeas/${project} HEAD:${branch}
git push https://${USER}:${PASSWORD}@github.com/Silverpeas/${project} --tags
"""
  }
}

@NonCPS
def getNextVersion(version) {
  String[] parts = version.split('\\.')
  parts.length < 3 ? "${parts[0]}.${(parts[1] as Integer) + 1}" : "${parts[0]}.${parts[1]}.${(parts[2] as Integer) + 1}"
}

