/*
 * This pipeline requires the following build parameter to be set:
 * BUILD_VERSION: the version number of an existing Silverpeas JCR Access Controller build from which the release will be performed
 */
node {
  catchError {
    def nexusRepo = 'https://www.silverpeas.org/nexus/content/repositories/builds'
    def gitRepo = 'github.com/Silverpeas/Silverpeas-JCR-AccessControl'
    def version
    def commit
    docker.image("silverpeas/silverbuild")
        .inside('-v $HOME/.m2/settings.xml:/home/silverbuild/.m2/settings.xml -v $HOME/.m2/settings-security.xml:/home/silverbuild/.m2/settings-security.xml -v $HOME/.gitconfig:/home/silverbuild/.gitconfig -v $HOME/.ssh:/home/silverbuild/.ssh -v $HOME/.gnupg:/home/silverbuild/.gnupg') {
      stage('Preparation') {
        copyArtifacts projectName: 'Silverpeas_Jackrabbit-JCA_AutoDeploy', flatten: true, selector: specific(params.BUILD)
        def lastBuild = readYaml file: 'build.yaml'
        version = lastBuild.release
        commit = lastBuild.commit
        sh "rm -rf *"
      }
      stage('Checkout Build version to release') {
        git credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59', poll: false, url: "https://${gitRepo}"
        sh """
git checkout ${commit}
git checkout -b release-${version}
"""
      }
      stage('Release') {
        echo "Release of JCR Access Control ${version}..."
        // release the project
        releaseProject gitRepo, commit, version
        prepareNextSnapshotVersion gitRepo, commit, version
      }
    }
  }
  step([$class: 'Mailer',
        notifyEveryUnstableBuild: true,
        recipients: "miguel.moquillon@silverpeas.org, yohann.chastagnier@silverpeas.org",
        sendToIndividuals: true])
}

def buildMavenProjectSnapshot(nextVersion) {
  sh """
sed -i -e "s/<next.release>[0-9.]\\+/<next.release>${nextVersion}/g" pom.xml
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${nextVersion}-SNAPSHOT
mvn clean install -Dmaven.test.skip=true
"""
}

def buildMavenProjectRelease(version) {
  sh """
mvn -U versions:set -DgenerateBackupPoms=false -DnewVersion=${version}
mvn clean deploy -Dmaven.test.skip=true -Prelease-sign-artifacts
"""
}

def prepareNextSnapshotVersion(gitRepo, commit, version) {
  echo "-> Prepare Next Snapshot version after ${version}"
  def nextVersion = getNextVersion(version)

  echo "Prepare the next development iteration for ${nextVersion}"
  sh "git checkout master"
  buildMavenProjectSnapshot(nextVersion)
  withCredentials([usernamePassword(credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59',
      usernameVariable: 'USER', passwordVariable: 'PASSWORD')]) {
    sh """
git commit -am "${version} has been released from commit ${commit} (build version ${params.BUILD_VERSION}). Prepare for development iteration of next version ${nextVersion}"
git push https://${USER}:${PASSWORD}@${gitRepo} HEAD:master
"""
  }
}

def releaseProject(gitRepo, commit, version) {
  echo "-> Release project to version ${version}"
  buildMavenProjectRelease(version)
  withCredentials([usernamePassword(credentialsId: 'cacc0467-7c85-41d1-bf4e-eaa470dd5e59',
      usernameVariable: 'USER', passwordVariable: 'PASSWORD')]) {
    sh """
git commit -am "Release of ${version}"
git tag ${version}
git push https://${USER}:${PASSWORD}@${gitRepo} --tags
"""
  }
}

@NonCPS
def getVersionNumber(buildVersion) {
  def matcher = buildVersion =~ '(.+)-build\\d+'
  matcher ? matcher[0][1] : null
}

@NonCPS
def getNextVersion(version) {
  String[] parts = version.split('\\.')
  "${parts[0]}.${(parts[1] as Integer) + 1}"
}